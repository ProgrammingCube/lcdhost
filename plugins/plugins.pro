#
# plugins.pro
#

TARGET = plugins
TEMPLATE = subdirs
CONFIG += ordered

defineTest(plugins_prf) {
    win32: system(echo $$quote($$1)>>$$PWD/plugins.prf)
    else: system(echo \'$$quote($$1)\'>>$$PWD/plugins.prf)
}

!exists($$PWD/plugins.prf) {
    warning("creating default $$PWD/plugins.prf")
    plugins_prf("$$LITERAL_HASH Generated by $$PWD/plugins.pro")
    plugins_prf("isEmpty(LCDHOST_PRF): LCDHOST_PRF=\"lcdhost.prf\"")
    plugins_prf("for( i, 1..10 ): !exists(\$\$PWD/\$\$LCDHOST_PRF): LCDHOST_PRF=../\$\$LCDHOST_PRF")
    plugins_prf("load(\$\$PWD/\$\$LCDHOST_PRF)")
}

load($$PWD/plugins.prf)
isEmpty(LH_PLUGINS_LIST): LH_PLUGINS_LIST=$$files(LH_*)
SUBDIRS += $$LH_PLUGINS_LIST
