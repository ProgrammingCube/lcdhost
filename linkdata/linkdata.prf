#
# linkdata.prf
#
# Johan Lindh <johan@linkdata.se> is the maintainer of this
# file and all the files in this directory and subdirectories.
# Do not commit here unless you have explicit approval to do so.
#
# If you find a bug or need a feature or change, submit an issue at
# http://code.google.com/p/lcdhost/issues/list
#

CONFIG *= qt threads
QT *= core gui network

# LH_DESTDIR must point to the root destination directory.
# This is usually set in ../lcdhost.prf
isEmpty(LH_DESTDIR): error("LH_DESTDIR is not set")

# Set default destinations paths based on platform
# and define LCDHOST_PLATFORM based on OS and release mode.
CONFIG(debug, debug|release): LCDHOST_PLATFORM = "d"
else: LCDHOST_PLATFORM = ""
win32 {
    LCDHOST_BINARIES = $$LH_DESTDIR
    LCDHOST_PLUGINS = $$LH_DESTDIR/plugins
    LCDHOST_PLATFORM=win32$$LCDHOST_PLATFORM
}
macx {
    LCDHOST_BINARIES = $$LH_DESTDIR/LCDHost.app/Contents/MacOS
    LCDHOST_PLUGINS = $$LH_DESTDIR/LCDHost.app/Contents/PlugIns
    LCDHOST_PLATFORM=mac64$$LCDHOST_PLATFORM
}
unix:!macx {
    LCDHOST_BINARIES = $$LH_DESTDIR
    LCDHOST_PLUGINS = $$LH_DESTDIR/plugins
    LCDHOST_PLATFORM=lin64$$LCDHOST_PLATFORM
}
DEFINES += LCDHOST_PLATFORM=\"\\\"$$LCDHOST_PLATFORM\\\"\"

# See if we have a reasonable default for DESTDIR based on TEMPLATE and CONFIG
# If nothing else fit, just dump it in LH_DESTDIR.
# In particular, the linkdata/ static libraries must end up there.
isEmpty(DESTDIR): contains(TEMPLATE,app): !contains(CONFIG,app_bundle): DESTDIR = $$LCDHOST_BINARIES
isEmpty(DESTDIR): contains(TEMPLATE,lib): !staticlib: DESTDIR = $$LCDHOST_PLUGINS
isEmpty(DESTDIR): DESTDIR = $$LH_DESTDIR

# Pull revision data from mercurial and put it in the
# CHANGESET and REVISION defines.
CHANGESET_REVISION = $$system($$quote(cd $$_PRO_FILE_PWD_ && hg log -l 1 --template \'{node}/{rev}\' .))
!isEmpty(CHANGESET_REVISION) {
    CHANGESET = $$section(CHANGESET_REVISION,"/",0,0)
    REVISION = $$section(CHANGESET_REVISION,"/",1,1)
    isEmpty( REVISION ): REVISION = 0
    DEFINES += CHANGESET=\"\\\"$$CHANGESET\\\"\"
    DEFINES += REVISION=$$REVISION
} else {
    warning("Can't get mercurial revison for" $$_PRO_FILE_PWD_)
}

# Add linkdata/ to the include path, and
# LH_DESTDIR to the static library search path.
INCLUDEPATH *= $$PWD
DEPENDPATH *= $$PWD
LIBS *= -L$$LH_DESTDIR

# API 5 compatibility CONFIG's
lh_api5_plugin {
    lh_api6: error($$_PRO_FILE_ ": Can't have both lh_api6 and lh_api5_plugin: INCLUDEPATH's will conflict")
    CONFIG += lh_api5
    hid|hidapi: CONFIG += lh_hidapi
    usb|libusb: CONFIG += lh_libusb
    INCLUDEPATH *= $$PWD/lh_api5
    DEPENDPATH *= $$PWD/lh_api5
    INCLUDEPATH *= $$PWD/lh_api5/QtSupport
    DEPENDPATH *= $$PWD/lh_api5/QtSupport
}

# Load library prf files
LH_LIB_LIST=$$files($$PWD/*)
for(FILE,LH_LIB_LIST): exists($${FILE}/$$basename(FILE).prf): load($${FILE}/$$basename(FILE).prf)
