#
# PluginsBase.prf
#
# Reads the mercurial revision information and puts it into 
# the CHANGESET and REVISION defines.
#
# Adds include paths and sources for building LCDHost plugins 
# with the Qt SDK.
#
# Supports the qmake CONFIG values 'lh_plugin', 'lh_qtplugin',
# 'hidapi' and 'libusb'.
#
# Johan Lindh <johan@linkdata.se> is the sole maintainer of this
# file and all the files in this directory and subdirectories.
# Do not commit here unless you have explicit approval to do so.
#
# If you find a bug or need a feature or change, submit an issue at
# http://code.google.com/p/lcdhost/issues/list
#

CHANGESET_REVISION = $$system($$quote(hg log -l 1 --template {node}/{rev} $$_PRO_FILE_PWD_))
!isEmpty(CHANGESET_REVISION) {
    CHANGESET = $$section(CHANGESET_REVISION,"/",0,0)
    REVISION = $$section(CHANGESET_REVISION,"/",1,1)
    isEmpty( REVISION ): REVISION = 0
    DEFINES += CHANGESET=\"\\\"$$CHANGESET\\\"\"
    DEFINES += REVISION=$$REVISION
}

lh_plugin {
    contains(QT,core) {
        include($$PWD/lh_plugin/lh_plugin.pro)
        CONFIG += lh_qtplugin
    } else {
        INCLUDEPATH += $$PWD/lh_plugin
        HEADERS += $$PWD/lh_plugin/lh_plugin.h
        SOURCES += $$PWD/lh_plugin/lh_plugin.c
    }
}

lh_qtplugin {
    LH_QT_SUPPORT_DIR = $$PWD/QtSupport

    LH_QT_SUPPORT_HEADERS = \
        $$LH_QT_SUPPORT_DIR/LH_Qt_bool.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_float.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_InputState.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_InputValue.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_int.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_QColor.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_QFileInfo.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_QFont.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_QImage.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_QProgressBar.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_QSlider.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_QString.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_QStringList.h \
        $$LH_QT_SUPPORT_DIR/LH_Qt_QTextEdit.h \
        $$LH_QT_SUPPORT_DIR/LH_QtCPU.h \
        $$LH_QT_SUPPORT_DIR/LH_QtDevice.h \
        $$LH_QT_SUPPORT_DIR/LH_QtInstance.h \
        $$LH_QT_SUPPORT_DIR/LH_QtNetwork.h \
        $$LH_QT_SUPPORT_DIR/LH_QtObject.h \
        $$LH_QT_SUPPORT_DIR/LH_QtPlugin.h \
        $$LH_QT_SUPPORT_DIR/LH_QtSetupItem.h

    LH_QT_SUPPORT_SOURCES = \
        $$LH_QT_SUPPORT_DIR/LH_QtCPU.cpp \
        $$LH_QT_SUPPORT_DIR/LH_QtDevice.cpp \
        $$LH_QT_SUPPORT_DIR/LH_QtInstance.cpp \
        $$LH_QT_SUPPORT_DIR/LH_QtNetwork.cpp \
        $$LH_QT_SUPPORT_DIR/LH_QtObject.cpp \
        $$LH_QT_SUPPORT_DIR/LH_QtPlugin.cpp \
        $$LH_QT_SUPPORT_DIR/LH_QtSetupItem.cpp

    INCLUDEPATH += $$LH_QT_SUPPORT_DIR
    HEADERS += $$LH_QT_SUPPORT_HEADERS
    SOURCES += $$LH_QT_SUPPORT_SOURCES
}

# Support the 'hidapi' CONFIG setting
hidapi {
    # We don't want warnings from 3rd party C code
    QMAKE_CFLAGS_WARN_ON = -w

    LH_HIDAPI_DIR = $$PWD/hidapi
    INCLUDEPATH += $$LH_HIDAPI_DIR
    HEADERS += $$LH_HIDAPI_DIR/hidapi.h

    win32 {
            SOURCES += $$LH_HIDAPI_DIR/win/hid.cpp
            LIBS += -lsetupapi
    }

    macx {
            SOURCES += $$LH_HIDAPI_DIR/osx/hid.c
            LIBS += -framework CoreFoundation -framework IOKit
    }

    unix:!macx {
            SOURCES += $$LH_HIDAPI_DIR/lin/hid.c
            LIBS += -ludev
    }
}

# Support the 'libusb' CONFIG setting
libusb {
    QMAKE_CFLAGS_WARN_ON = -w

    LH_LIBUSB_DIR = $$PWD/libusb
    INCLUDEPATH += $$LH_LIBUSB_DIR
    HEADERS += $$LH_LIBUSB_DIR/libusb.h
    SOURCES += $$LH_LIBUSB_DIR/core.c $$LH_LIBUSB_DIR/descriptor.c $$LH_LIBUSB_DIR/io.c $$LH_LIBUSB_DIR/sync.c

    win32 {
            DEFINES += OS_WINDOWS
            SOURCES += \
                $$LH_LIBUSB_DIR/os/windows_usb.c \
                $$LH_LIBUSB_DIR/os/threads_windows.c \
                $$LH_LIBUSB_DIR/os/poll_windows.c
            LIBS += -lole32 -lsetupapi -lcfgmgr32
    }

    macx {
            DEFINES += OS_DARWIN THREADS_POSIX HAVE_SYS_TIME_H HAVE_POLL_H
            SOURCES += $$LH_LIBUSB_DIR/os/darwin_usb.c
            LIBS += -framework CoreFoundation -framework IOKit
    }

    unix:!macx {
            DEFINES += OS_LINUX THREADS_POSIX HAVE_SYS_TIME_H HAVE_POLL_H
            SOURCES += $$LH_LIBUSB_DIR/os/linux_usbfs.c
    }
}
