#
# PluginsBase.prf
#
# Reads the mercurial revision information and puts it into 
# the CHANGESET and REVISION defines.
#
# Adds include paths and sources for building LCDHost plugins 
# with the Qt SDK.
#
# Supports the qmake CONFIG values 'lh_plugin', 'lh_qtplugin',
# 'hidapi' and 'libusb'.
#
# Johan Lindh <johan@linkdata.se> is the sole maintainer of this
# file and all the files in this directory and subdirectories.
# Do not commit here unless you have explicit approval to do so.
#
# If you find a bug or need a feature or change, submit an issue at
# http://code.google.com/p/lcdhost/issues/list
#

CHANGESET_REVISION = $$system($$quote(hg log -l 1 --template {node}/{rev} $$_PRO_FILE_PWD_))
!isEmpty(CHANGESET_REVISION) {
    CHANGESET = $$section(CHANGESET_REVISION,"/",0,0)
    REVISION = $$section(CHANGESET_REVISION,"/",1,1)
    isEmpty( REVISION ): REVISION = 0
    DEFINES += CHANGESET=\"\\\"$$CHANGESET\\\"\"
    DEFINES += REVISION=$$REVISION
}

lh_api5 {
    API5_DIR = $$PWD/lh_api5
    INCLUDEPATH += $$API5_DIR
    HEADERS += $$API5_DIR/lh_plugin.h
    SOURCES += $$API5_DIR/lh_plugin.c
    contains(QT,core) {
        HEADERS += \
            $$API5_DIR/LH_Qt_bool.h \
            $$API5_DIR/LH_Qt_float.h \
            $$API5_DIR/LH_Qt_InputState.h \
            $$API5_DIR/LH_Qt_InputValue.h \
            $$API5_DIR/LH_Qt_int.h \
            $$API5_DIR/LH_Qt_QColor.h \
            $$API5_DIR/LH_Qt_QFileInfo.h \
            $$API5_DIR/LH_Qt_QFont.h \
            $$API5_DIR/LH_Qt_QImage.h \
            $$API5_DIR/LH_Qt_QProgressBar.h \
            $$API5_DIR/LH_Qt_QSlider.h \
            $$API5_DIR/LH_Qt_QString.h \
            $$API5_DIR/LH_Qt_QStringList.h \
            $$API5_DIR/LH_Qt_QTextEdit.h \
            $$API5_DIR/LH_QtCPU.h \
            $$API5_DIR/LH_QtDevice.h \
            $$API5_DIR/LH_QtInstance.h \
            $$API5_DIR/LH_QtNetwork.h \
            $$API5_DIR/LH_QtObject.h \
            $$API5_DIR/LH_QtPlugin.h \
            $$API5_DIR/LH_QtSetupItem.h

        SOURCES += \
            $$API5_DIR/LH_QtCPU.cpp \
            $$API5_DIR/LH_QtDevice.cpp \
            $$API5_DIR/LH_QtInstance.cpp \
            $$API5_DIR/LH_QtNetwork.cpp \
            $$API5_DIR/LH_QtObject.cpp \
            $$API5_DIR/LH_QtPlugin.cpp \
            $$API5_DIR/LH_QtSetupItem.cpp
    }
}

# Support the 'hidapi' CONFIG setting
hidapi {
    # We don't want warnings from 3rd party C code
    QMAKE_CFLAGS_WARN_ON = -w

    LH_HIDAPI_DIR = $$PWD/hidapi
    INCLUDEPATH += $$LH_HIDAPI_DIR
    HEADERS += $$LH_HIDAPI_DIR/hidapi.h

    win32 {
            SOURCES += $$LH_HIDAPI_DIR/win/hid.cpp
            LIBS += -lsetupapi
    }

    macx {
            SOURCES += $$LH_HIDAPI_DIR/osx/hid.c
            LIBS += -framework CoreFoundation -framework IOKit
    }

    unix:!macx {
            SOURCES += $$LH_HIDAPI_DIR/lin/hid.c
            LIBS += -ludev
    }
}

# Support the 'libusb' CONFIG setting
libusb {
    QMAKE_CFLAGS_WARN_ON = -w

    LH_LIBUSB_DIR = $$PWD/libusb
    INCLUDEPATH += $$LH_LIBUSB_DIR
    HEADERS += $$LH_LIBUSB_DIR/libusb.h
    SOURCES += $$LH_LIBUSB_DIR/core.c $$LH_LIBUSB_DIR/descriptor.c $$LH_LIBUSB_DIR/io.c $$LH_LIBUSB_DIR/sync.c

    win32 {
            DEFINES += OS_WINDOWS
            SOURCES += \
                $$LH_LIBUSB_DIR/os/windows_usb.c \
                $$LH_LIBUSB_DIR/os/threads_windows.c \
                $$LH_LIBUSB_DIR/os/poll_windows.c
            LIBS += -lole32 -lsetupapi -lcfgmgr32
    }

    macx {
            DEFINES += OS_DARWIN THREADS_POSIX HAVE_SYS_TIME_H HAVE_POLL_H
            SOURCES += $$LH_LIBUSB_DIR/os/darwin_usb.c
            LIBS += -framework CoreFoundation -framework IOKit
    }

    unix:!macx {
            DEFINES += OS_LINUX THREADS_POSIX HAVE_SYS_TIME_H HAVE_POLL_H
            SOURCES += $$LH_LIBUSB_DIR/os/linux_usbfs.c
    }
}
