#
# PluginsBase.prf
#
# Johan Lindh <johan@linkdata.se> is the sole maintainer of this
# file and all the files in this directory and subdirectories.
# Do not commit here unless you have explicit approval to do so.
#
# If you find a bug or need a feature or change, submit an issue at
# http://code.google.com/p/lcdhost/issues/list
#

isEmpty(LH_DESTDIR): error("LH_DESTDIR is not set")
isEmpty(DESTDIR): DESTDIR=$$LH_DESTDIR

CHANGESET_REVISION = $$system($$quote(cd $$_PRO_FILE_PWD_ && hg log -l 1 --template \'{node}/{rev}\' .))
!isEmpty(CHANGESET_REVISION) {
    CHANGESET = $$section(CHANGESET_REVISION,"/",0,0)
    REVISION = $$section(CHANGESET_REVISION,"/",1,1)
    isEmpty( REVISION ): REVISION = 0
    DEFINES += CHANGESET=\"\\\"$$CHANGESET\\\"\"
    DEFINES += REVISION=$$REVISION
} else {
    warning("Can't get mercurial revison for" $$_PRO_FILE_PWD_)
}

INCLUDEPATH += $$PWD
LIBS += -L$$LH_DESTDIR

# API 5 compat layer
lh_plugin {
    CONFIG -= lh_api6
    hidapi: CONFIG += lh_hidapi
    LIBS += -llh_api6 -llh_api5
    unix:POST_TARGETDEPS += $$LH_DESTDIR/liblh_api6.a $$LH_DESTDIR/liblh_api5.a
    PRECOMPILED_HEADER = $$PWD/lh_api5/lh_api5.h
}

lh_api6 {
    LIBS += -llh_api6
    unix:POST_TARGETDEPS += $$LH_DESTDIR/liblh_api6.a
    PRECOMPILED_HEADER = $$PWD/lh_api6/lh_api6.h
}

lh_hidapi {
    LIBS += -llh_hidapi
    unix:POST_TARGETDEPS += $$LH_DESTDIR/liblh_hidapi.a
    win32:LIBS += -lsetupapi
    macx: LIBS += -framework CoreFoundation -framework IOKit
    unix:!macx: LIBS += -ludev
}

lh_libusb {
    unix:POST_TARGETDEPS += $$LH_DESTDIR/liblh_hidusb.a
    LIBS += -llh_libusb

    win32 {
            DEFINES += OS_WINDOWS
            LIBS += -lole32 -lsetupapi -lcfgmgr32
    }

    macx {
            DEFINES += OS_DARWIN THREADS_POSIX HAVE_SYS_TIME_H HAVE_POLL_H
            LIBS += -framework CoreFoundation -framework IOKit
    }

    unix:!macx {
            DEFINES += OS_LINUX THREADS_POSIX HAVE_SYS_TIME_H HAVE_POLL_H
    }
}

