#
# Plugins.pro
#
# Johan Lindh <johan@linkdata.se> is the maintainer of this file. 
# Do not alter to this file unless you have explicit approval.
#
# If you find a bug or need a feature or change, submit an issue at
# http://code.google.com/p/lcdhost/issues/list
#
# To control which plugins get built you must create the file
# "PluginsConfig.prf" and define LH_PLUGINS_LIST in that. If
# you leave LH_PLUGINS_LIST empty, it will build them all.
#
# This file will create a default "PluginsConfig.prf" for you
# if it is missing when qmake runs.
#
# You should consider signing your plugins. Please read
# "linkdata/SignPlugin.pri" for details on how to do so.
#

!exists($$PWD/PluginsConfig.prf) {
    warning("Plugins.pro: Missing PluginsConfig.prf, creating a default one")
    system($$quote(echo $$LITERAL_HASH Generated by Plugins.pro>> $$PWD/PluginsConfig.prf))
    system($$quote(echo LH_PLUGINS_LIST= $$LITERAL_HASH Leave empty to build all plugins>> $$PWD/PluginsConfig.prf))
    system($$quote(echo load(\$\$PWD/linkdata/PluginsBase.prf)>> $$PWD/PluginsConfig.prf))
    system($$quote(echo CONFIG(debug):DESTDIR=$$PWD/Debug>> $$PWD/PluginsConfig.prf))
    system($$quote(echo else:DESTDIR=$$PWD/Release>> $$PWD/PluginsConfig.prf))
    system($$quote(echo exists(\$\$PWD/private.pem) { $$LITERAL_HASH You should keep it outside the repo>> $$PWD/PluginsConfig.prf))
    system($$quote(echo     SIGNATURE_PRIVATE_FILE=\$\$PWD/private.pem>> $$PWD/PluginsConfig.prf))
    system($$quote(echo     SIGNATURE_PUBLIC_URL=http://my.website/myplugin/public.pem>> $$PWD/PluginsConfig.prf))
    system($$quote(echo     include(\$\$PWD/linkdata/SignPlugin.pri)>> $$PWD/PluginsConfig.prf))
    system($$quote(echo }>> $$PWD/PluginsConfig.prf))
    error("Please review $$PWD/PluginsConfig.prf, make adjustments as needed, and then rebuild")
}

load($$PWD/PluginsConfig.prf)
isEmpty(LH_PLUGINS_LIST): LH_PLUGINS_LIST=$$files(LH_*)

TARGET = Plugins
TEMPLATE = subdirs
CONFIG += ordered
SUBDIRS = linkdata
SUBDIRS += $$LH_PLUGINS_LIST
